--- TODO Fred program structure to specify

fmod PROGRAM is
  
  inc COMPONENT-TYPE .
  inc ID-COMPONENT-BEHAVIOR .
  inc CONNECTION .

  sorts Program . 
  pr LIST{Instruction} .
  subsort List{Instruction} < Program . 

  var Rx : Program .
  var L : Connection .
  var Inst : Instruction .
  var Id1 : IdInstance .
  var IdBeh : IdBehavior .
  var Beh : Behavior .

  op add(_,_) : IdInstance ComponentType -> Instruction [ctor] .
  op del(_) : IdInstance -> Instruction [ctor] .
  op pushB(_,_,_) : IdInstance Behavior IdBehavior -> Instruction [ctor] .
  op con(_) : Connection -> Instruction [ctor] .
  op dcon(_) : Connection -> Instruction [ctor] .
  op wait(_,_) : IdInstance IdBehavior -> Instruction [ctor] .

 --- some operations related to program used later
  op noDcon(_,_) :  Connection Program -> Bool .  
  ---check for a given connection if there is a dcon instrcution in a given program
  eq noDcon(L,nil) = true .
  eq noDcon(L,Inst  Rx) = (Inst =/= dcon(L) and noDcon(L,Rx)) .

  op onDcon(_,_) : Connection Program -> Bool .
  op onDcon(_,_) : Connection Program -> Bool .  
  --- used to check that the dcon instruction is the current instruction or has already been executed
  --- then it is the same as checking that the remaining program does not contain dcon after the current instruction
  --- this reasoning is correct if we assume that Concerto programs are well constructed.
  eq onDcon(L,nil) = true .
  eq onDcon(L,Inst Rx) = noDcon(L,Rx) .
  
  op onPushB(_,_,_) : Instruction IdInstance IdBehavior -> Bool .  
  --- checks whether the current instruction is a push instruction for a given IdInstance and IdBehavior
  eq onPushB(pushB(Id1,Beh,IdBeh), Id1,IdBeh ) = true .
  eq onPushB(Inst, Id1,IdBeh ) = false [owise] .

  op noPushB(_,_,_) : IdInstance IdBehavior Program -> Bool .  
  ---checks for a given program if there is a pushB instruction
  eq noPushB(Id1,IdBeh, nil) = true .
  ceq noPushB(Id1,IdBeh,Inst Rx) = noPushB(Id1,IdBeh,Rx) if (not onPushB(Inst,Id1,IdBeh)) .
  eq noPushB(Id1,IdBeh,Inst Rx) = false [owise] .
  
endfm