--- Concerto-D Reconfiguration Programs ---

fmod PROGRAM is
  
  inc COMPONENT-TYPE .
  inc ID-COMPONENT-BEHAVIOR .
  inc CONNECTION .

  sorts Program . 
  pr LIST{Action} .
  subsort List{Action} < Program . 

  var RP : Program .
  var C : Connection .
  var A : Action .
  var Id1 : Id .
  var IdBeh : Id .
  var Beh : Behavior .

  op add(_,_) : Id ComponentType -> Action [ctor] .
  op del(_) : Id -> Action [ctor] .
  op pushB(_,_,_) : Id Behavior Id -> Action [ctor] .
  op con(_) : Connection -> Action [ctor] .
  op dcon(_) : Connection -> Action [ctor] .
  op wait(_,_) : Id Id -> Action [ctor] .

  --- some operations related to program used later
  op noDcon(_,_) :  Connection Program -> Bool .  
  --- checks for a given connection whether there is a dcon instruction
  --- in a given program
  eq noDcon(C,nil) = true .
  eq noDcon(C,A  RP) = (A =/= dcon(C) and noDcon(C,RP)) .

  op onDisconnectLocal(_,_) : Connection Program -> Bool .
  op onDisconnectLocal(_,_) : Connection Program -> Bool .  
  --- used to check that the dcon instruction is the current instruction or has
  --- already been executed then it is the same as checking that the remaining 
  --- program does not contain dcon after the current instruction
  --- this reasoning is correct if we assume that Concerto programs are well 
  --- formed.
  eq onDisconnectLocal(C,nil) = true .
  eq onDisconnectLocal(C,A RP) = noDcon(C,RP) .
  
  op onPushB(_,_,_) : Action Id Id -> Bool .  
  --- checks whether the current instruction is a push instruction 
  --- for a given Id and Id
  eq onPushB(pushB(Id1,Beh,IdBeh), Id1,IdBeh ) = true .
  eq onPushB(A, Id1,IdBeh ) = false [owise] .

  op isCompletedLocal(_,_,_) : Id Id Program -> Bool .  
  --- checks for a given program if there is a pushB instruction
  eq isCompletedLocal(Id1,IdBeh, nil) = true .
  ceq isCompletedLocal(Id1,IdBeh,A RP) = 
    isCompletedLocal(Id1,IdBeh,RP) if (not onPushB(A,Id1,IdBeh)) .
  eq isCompletedLocal(Id1,IdBeh,A RP) = false [owise] .
  
endfm