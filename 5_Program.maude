fmod PROGRAM is
  
  inc COMPENENT-TYPE .
  inc IDENT-COMPENENT-BEHAVIOR .
  inc CONNECTION .
  sorts Program Instruction . ---define new sorts

  var Prog : Program .
  var l : Connection .
  var inst : Instruction .
  var idcx : IdentInstance .
  var idbehx : IdentBehavior .
  var beh : Behavior .

  op add(_,_) : IdentInstance ComponentType -> Instruction .
  op del(_) : IdentInstance -> Instruction .
  op pushB(_,_,_) : IdentInstance Behavior IdentBehavior -> Instruction .
  op con(_) : Connection -> Instruction .
  op dcon(_) : Connection -> Instruction .
  op wait(_,_) : IdentInstance IdentBehavior -> Instruction .
  op [] : -> Program [ctor] .  ---operator is a constructor
  op _._ : Instruction Program -> Program [ctor] . ---operator is a constructor

 --- some operations related to Program used later
  op NotDcon(_,_) :  Connection Program -> Bool .  
  ---check for a given connection, there is no dcon instrcution in a given program
 
  eq NotDcon(l,[]) = true .
  eq NotDcon(l,inst . Prog) = (inst =/= dcon(l) and NotDcon(l,Prog)) .

  op disconnect(_,_) : Connection Program -> Bool .  
  --- used to check that the dcon instruction is the current instruction or has already been executed
  ---then it is the same as checking that the remaining program does not contain dcon after the current instruction
  --- this reasoning is correct if we assume that Concerto programs are well constructed.
  eq disconnect(l,[]) = true .
  eq disconnect(l,inst . Prog) = NotDcon(l,Prog) .
  
  op InstructionIspushB(_,_,_) : Instruction IdentInstance IdentBehavior -> Bool .  
  --- checks whether the current instruction is a push instruction for a given IdentInstance and IdentBehavior

  eq InstructionIspushB(pushB(idcx,beh,idbehx), idcx,idbehx ) = true .
  eq InstructionIspushB(inst, idcx,idbehx ) = false [owise] .

  op NotPushBProg(_,_,_) : IdentInstance IdentBehavior Program -> Bool .  
  ---checks that in a given program there is no push behavior instruction for IdentInstance and IdentBehavior
  
  eq NotPushBProg(idcx,idbehx, []) = true .
  ceq NotPushBProg(idcx,idbehx,inst . Prog) = NotPushBProg(idcx,idbehx,Prog) if (not InstructionIspushB(inst,idcx,idbehx)) .
  ceq NotPushBProg(idcx,idbehx,inst . Prog) = false if (InstructionIspushB(inst,idcx,idbehx)) .
  
endfm