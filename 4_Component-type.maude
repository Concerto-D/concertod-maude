fmod COMPONENT-TYPE is
  
  inc STATION-PLACE .
  inc TRANSITION-TRANSITION-ENDING .
  inc BEHAVIOR .
  inc PORT .
  inc DECLARATION-SORTS . 
  inc MARKING . 
  
  sorts ComponentType GroupUses GroupProvides .  
  pr SET{GroupUse} .
  subsort Set{GroupUse} < GroupUses . 
  pr SET{GroupProvide} .
  subsort Set{GroupProvide} < GroupProvides . 

  vars Ps Ps1 : Places .
  var Q : InitialPlace .
  var SPs : StationPlaces .
  var Ts  : Transitions . 
  var Bs : Behaviors .
  var GU : GroupUses .
  var GP : GroupProvides .
  vars Use Use1 : UsePort .
  vars Pr Pr1 : ProvidePort .
  var Beh  : Behavior .

  op g(_?_) : UsePort Places -> GroupUse [ctor] . 
  --- GroupUse is defined as tuple : the use port and a group (set) of places
  op g(_!_) : ProvidePort Places -> GroupProvide [ctor] . 
  --- GroupProvide is defined as tuple : the Provide port and a group (set) of places
  op { places: _,initial: _,stationPlaces: _,transitions: _,behaviors: _,groupUses: _,groupProvides: _ } : 
  Places InitialPlace StationPlaces Transitions Behaviors GroupUses GroupProvides -> ComponentType [ctor] . 

  --- some operations related to ComponentType used later
  op getInitialPlace(_) : ComponentType -> Place .
  --- get the initial place of component type
  eq getInitialPlace ({ places: Ps,initial: Q,stationPlaces: SPs,transitions: Ts,behaviors: Bs,groupUses: GU,groupProvides: GP }) = Q .

  op getStationPlaces(_) : ComponentType -> StationPlaces . 
  --- get the set of tuples (station; place)
  eq getStationPlaces ({ places: Ps,initial: Q,stationPlaces: SPs,transitions: Ts,behaviors: Bs,groupUses: GU,groupProvides: GP }) = SPs .

  op getUsePorts(_) : ComponentType -> GroupUses . 
  --- get the set of the use ports of the component
  eq getUsePorts ({ places: Ps,initial: Q,stationPlaces: SPs,transitions: Ts,behaviors: Bs,groupUses: GU,groupProvides: GP }) = GU .

  op getPlacesOfUse(_,_) : UsePort GroupUses -> Places . 
  --- get places bound to a use port
  eq getPlacesOfUse(Use,g(Use ? Ps),GU) = Ps . 
  eq getPlacesOfUse(Use,GU) = empty [owise] . 
   
  op getPlacesOfProvide(_,_) : ProvidePort GroupProvides -> Places . 
  --- get places bound to a provide port
  eq getPlacesOfProvide(Pr,(g(Pr ! Ps),GP)) = Ps  . 
  eq getPlacesOfProvide(Pr,GP) = empty [owise] . 
   
  op getPlacesOfPort(_,_) : Port ComponentType -> Places . 
  --- to get places bound to a port use or provide
  eq getPlacesOfPort(Use,{ places: Ps,initial: Q,stationPlaces: SPs,transitions: Ts,behaviors: Bs,groupUses: GU,groupProvides: GP }) = getPlacesOfUse(Use,GU)  .
  eq getPlacesOfPort(Pr,{ places: Ps,initial: Q,stationPlaces: SPs,transitions: Ts,behaviors: Bs,groupUses: GU,groupProvides: GP } ) = getPlacesOfProvide(Pr,GP)  .

  op isBehaviorInComponent(_,_) : Behavior ComponentType -> Bool . 
  --- check whether a behavior is used in a ComponentType
  eq isBehaviorInComponent(Beh,{ places: Ps,initial: Q,stationPlaces: SPs,transitions: Ts,behaviors: Bs,groupUses: GU,groupProvides: GP }) = Beh in Bs .
  
  op getMarkingOfGroup(_,_) : Places ComponentType -> Marking .   
  --- gives marked elements (places, transitions, TransitionEndings) of a group of places
  eq getMarkingOfGroup(Ps1,{ places: Ps,initial: Q,stationPlaces: SPs,transitions: Ts,behaviors: Bs,groupUses: GU,groupProvides: GP }) = 
  m(Ps1, transitionsOfPlacesWithRespectToStations(Ps1,Ts,getStationsfromGroup(Ps1,SPs)), 
  getEndingsfTransitions(transitionsOfPlacesWithRespectToStations(Ps1,Ts,getStationsfromGroup(Ps1,SPs)))) .

endfm




