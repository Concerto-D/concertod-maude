-----------------------------------------------------some ops Used in the operating rules -----------------------------------------------------------
--------------------------------------to determine messages related to external functions used locally ----------------------------------------------

fmod COLLECT-EXTERNAL-MESSAGES-FIRING is

  inc CONCERTO-D-CONFIGURATION .

  var IdIx : IdentInstances . 
  var Ix : Instances .
  var L : Connections .
  var Val : BoolWithBot .
  var Use : UsePort .
  var Pro : ProvidePort .
  vars Id1 Id2 : IdentInstance .
  var NewMark : Marking .
  var Mx : MsgToUses .
  var Hx : SentHistorys .
  var QSx : Qsend .

  op collectFunctionFiring(_,_,_,_,_,_,_) : IdentInstances Marking Instances Connections MsgToUses SentHistorys Qsend -> Qsend .
  --- used to determine msg related to external functions that allow to apply the rule firing transition. 
  --- it aims to build a queue of external exprActive messages for use ports whose current valuation in MsgToUses does not allow the rule to be applied; 
  --- in other words, the evaluation of these messages is true or bot 
  --- knowing that the provide port linked to thier use port wants to change the status of its provide port from active to inactive.
  --- in this case, we add the message exprActive of each use port to the queue of external information, 
  --- on condition that the request for this message is not currently being processed.
  --- this last condition is verified if exprActive doesn't belong to either SentHistorys or Qsend.
  eq collectFunctionFiring(IdIx,NewMark,Ix,empty,Mx,Hx,QSx) = nil .
  ceq collectFunctionFiring(IdIx,NewMark,Ix,(Id1,Use,Id2,Pro),L,Mx,Hx,QSx) = 
  append(exprActive(Id1,Use),collectFunctionFiring(IdIx,NewMark,Ix,L,Mx,Hx,QSx))
  if
  (   
  (not exprActive(Id1,Use) in Hx) and 
  (not (occurs(exprActive(Id1,Use),QSx)))  and
  (not (Id1 in IdIx)) and
  ( evaluation(IdIx,exprActive(Id1,Use),Mx,empty,empty,nil )   == true  or evaluation(IdIx,exprActive(Id1,Use),Mx,empty,empty,nil )  == bot ) and
  ( (localEvaluation(exprActive(Id2,Pro),Ix,empty,nil ) == true) and (activeMarking(instanceOfIdent(Ix,Id2),Pro,NewMark) == false ))  
  ) .
  eq collectFunctionFiring(IdIx,NewMark,Ix,(Id1,Use,Id2,Pro),L,Mx,Hx,QSx) = collectFunctionFiring(IdIx,NewMark,Ix,L,Mx,Hx,QSx) [owise] .

endfm


--- we follow a similar reasoning to construct the messages for the external functions 
--- needed to apply the rules: entering place, wait and disconnect.
fmod COLLECT-EXTERNAL-MESSAGES-ENTERING-PLACE is

  inc CONCERTO-D-CONFIGURATION .

  var P : Place .
  var Ps : Places .
  var GUx :  GroupUses .
  vars I I' : Instance .
  var Ix : Instances .
  var L : Connections .
  var Mx : MsgToUses .
  vars Use1 Use : UsePort .
  var Pro : ProvidePort .
  vars IdentIns Id1 Id2 : IdentInstance .
  var Hx : SentHistorys .
  var QSx : Qsend .  
  var IdIx : IdentInstances .   

  op collectFunctionsEnteringPlace(_,_,_,_,_,_,_,_,_,_) : 
  IdentInstances Place GroupUses Instance Instance Instances Connections MsgToUses SentHistorys Qsend -> Qsend .
  eq collectFunctionsEnteringPlace(IdIx,P,empty,I,I',Ix,L,Mx,Hx,QSx) = nil .
  ceq collectFunctionsEnteringPlace(IdIx,P,(Use ! Ps),GUx,I,I',Ix,L,Mx,Hx,QSx) = 
  append(collectConnected(IdIx,Use,identOfInstance(I),L,Mx,Hx,QSx),append(collectAllowed(IdIx,Use,identOfInstance(I),I,Ix,L,Mx,Hx,QSx), collectFunctionsEnteringPlace(IdIx,P,GUx,I,I',Ix,L,Mx,Hx,QSx))) 
  if  ((P in Ps) and active(I,Use) == false and active(I',Use) == true  ).   
  eq collectFunctionsEnteringPlace(IdIx,P,(Use ! Ps),GUx,I,I',Ix,L,Mx,Hx,QSx) = collectFunctionsEnteringPlace(IdIx,P,GUx,I,I',Ix,L,Mx,Hx,QSx) [owise] .

  op collectConnected(_,_,_,_,_,_,_) : IdentInstances UsePort IdentInstance Connections MsgToUses SentHistorys Qsend -> Qsend . 
  eq collectConnected(IdIx,Use,Id1,empty,Mx,Hx,QSx) = nil .
  ceq collectConnected(IdIx,Use,Id1,(Id1,Use,Id2,Pro),L,Mx,Hx,QSx) = exprIsConnected(Id2,(Id1,Use,Id2,Pro))
  if(
    (not (exprIsConnected(Id2,(Id1,Use,Id2,Pro)) in Hx)) and 
    (not (occurs(exprIsConnected(Id2,(Id1,Use,Id2,Pro)),QSx)))  and
    (not (Id2 in IdIx)) and
    ( evaluation(IdIx,exprIsConnected(Id2,(Id1,Use,Id2,Pro)),Mx,empty,empty,nil )   == false  or evaluation(IdIx,exprIsConnected(Id2,(Id1,Use,Id2,Pro)),Mx,empty,empty,nil )   == bot )
    ) .
  eq collectConnected(IdIx,Use1,IdentIns,(Id1,Use,Id2,Pro),L,Mx,Hx,QSx) = collectConnected(IdIx,Use1,IdentIns,L,Mx,Hx,QSx) [owise] .

  op collectAllowed(_,_,_,_,_,_,_,_) : IdentInstances UsePort  IdentInstance Instances Connections MsgToUses SentHistorys Qsend -> Qsend . 
  eq collectAllowed(IdIx,Use,Id1,Ix,empty,Mx,Hx,QSx) = nil .
  ceq collectAllowed(IdIx,Use,Id1,Ix,(Id1,Use,Id2,Pro),L,Mx,Hx,QSx) = exprRefusing(Id2,Pro)
  if (
      (not exprRefusing(Id2,Pro) in Hx) and 
      (not (occurs(exprRefusing(Id2,Pro),QSx)))  and
      (not (Id2 in IdIx)) and  
      ( evaluation(IdIx,exprRefusing(Id2,Pro),Mx,empty,empty,nil )   == true  or evaluation(IdIx,exprRefusing(Id2,Pro),Mx,empty,empty,nil)   == bot )   
    ) .
  eq collectAllowed(IdIx,Use1,IdentIns,Ix,(Id1,Use,Id2,Pro),L,Mx,Hx,QSx) = collectAllowed(IdIx,Use1,IdentIns,Ix,L,Mx,Hx,QSx) [owise] .

endfm

fmod COLLECT-EXTERNAL-MESSAGES-WAIT is

  inc CONCERTO-D-CONFIGURATION .

  var Val : BoolWithBot .
  var Id1 : IdentInstance .
  var IdBeh : IdentBehavior .
  var Mx : MsgToUses .
  var Hx : SentHistorys .
  var QSx : Qsend .
  var M : Msg .
  var IdIx : IdentInstances .   

  op collectFunctionWait(_,_,_,_,_,_) : IdentInstances IdentInstance IdentBehavior MsgToUses SentHistorys Qsend -> Qsend .
  ceq collectFunctionWait(IdIx,Id1,IdBeh,Mx,Hx,QSx) = nil if (Id1 in IdIx) .
  ceq collectFunctionWait(IdIx,Id1,IdBeh,extern(M ; Val),Mx,Hx,QSx) =  collectFunctionWait(IdIx,Id1,IdBeh,Mx,Hx,QSx) 
  if ((M =/= exprCompleted(Id1,IdBeh))  and (not (Id1 in IdIx)) ) .
 
  eq collectFunctionWait(IdIx,Id1,IdBeh,extern(exprCompleted(Id1,IdBeh) ; Val),Mx,Hx,QSx) = 
  if (( Val == true ) or (exprCompleted(Id1,IdBeh) in Hx) or (occurs(exprCompleted(Id1,IdBeh),QSx) ) )  then nil else exprCompleted(Id1,IdBeh) fi .
  
  eq collectFunctionWait(IdIx,Id1,IdBeh,empty,Hx,QSx) =  
  if ((not (exprCompleted(Id1,IdBeh) in Hx)) and  (not (occurs(exprCompleted(Id1,IdBeh),QSx)))) then exprCompleted(Id1,IdBeh) else nil fi . 

endfm

fmod COLLECT-EXTERNAL-MESSAGES-DISCONNECT is

  inc CONCERTO-D-CONFIGURATION .

  var Val : BoolWithBot .
  var Use : UsePort .
  var Pro : ProvidePort .
  vars  Id1 Id2 : IdentInstance .
  var Mx : MsgToUses .
  var Hx : SentHistorys .
  var QSx : Qsend .
  var M : Msg .
  var IdIx : IdentInstances . 
  var L : Connection .  

  op collectFunctionsDiscon(_,_,_,_,_) : IdentInstances Connection MsgToUses SentHistorys Qsend -> Qsend .
  ceq collectFunctionsDiscon(IdIx,(Id1,Use,Id2,Pro),Mx,Hx,QSx) = append(activeCollect(Id1,Use,Mx,Hx,QSx),(disconCollect(Id1,(Id1,Use,Id2,Pro),Mx,Hx,QSx))) 
  if (not (Id1 in IdIx) ) .
  ceq collectFunctionsDiscon(IdIx,(Id1,Use,Id2,Pro),Mx,Hx,QSx) = disconCollect(Id2,(Id1,Use,Id2,Pro),Mx,Hx,QSx) 
  if (not (Id2 in IdIx) ) .
  eq collectFunctionsDiscon(IdIx,(Id1,Use,Id2,Pro),Mx,Hx,QSx) = nil [owise] .

  op activeCollect(_,_,_,_,_) : IdentInstance UsePort MsgToUses SentHistorys Qsend -> Qsend .
  ceq activeCollect(Id1,Use,extern(M ; Val),Mx,Hx,QSx) =  activeCollect(Id1,Use,Mx,Hx,QSx) if (M =/= exprActive(Id1,Use)) .
  eq activeCollect(Id1,Use,extern(exprActive(Id1,Use) ; Val),Mx,Hx,QSx) = 
  if 
  (( Val == false ) or (exprActive(Id1,Use) in Hx) or (exprActive(Id1,Use) in QSx) ) then nil else exprActive(Id1,Use) fi .
  eq activeCollect(Id1,Use,empty,Hx,QSx) =  
  if ((not (exprActive(Id1,Use) in Hx)) and  (not (occurs(exprActive(Id1,Use),QSx)))) then exprActive(Id1,Use) else nil fi . 

  op disconCollect(_,_,_,_,_) : IdentInstance Connection MsgToUses SentHistorys Qsend -> Qsend .
  ceq disconCollect(Id1,L,extern(M ; Val),Mx,Hx,QSx) =  disconCollect(Id1,L,Mx,Hx,QSx) if (M =/= exprDisconnect(Id1,L)) .
  eq disconCollect(Id1,L,extern(exprDisconnect(Id1,L) ; Val),Mx,Hx,QSx) = 
  if 
  (( Val == true ) or (exprDisconnect(Id1,L) in Hx) or (exprDisconnect(Id1,L) in QSx) ) then nil else exprDisconnect(Id1,L) fi .
  eq disconCollect(Id1,L,empty,Hx,QSx) =  
  if ((not (exprDisconnect(Id1,L) in Hx)) and  (not (occurs(exprDisconnect(Id1,L),QSx)))) then exprDisconnect(Id1,L) else nil fi . 

endfm



