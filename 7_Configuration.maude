
fmod CONCERTO-D-CONFIGURATION is 

  inc MESSAGE .
  inc INSTANCE .
  inc PROGRAM .
  inc CONNECTION .
  inc DECLARATION-SORTS . 

  pr SET{Request} . 
  pr MAP{Request, Bool} .
  pr LIST{Request} . 
  pr LIST{Message} .

  var Is : Instances .
  var I1 : Instance .
  var Ids : IdInstances .
  var IdBeh : IdBehavior .
  var Ct : ComponentType .
  var Port : Port .
  var Cs : Connections .
  var RP : Program .
  var C : Connection .
  var Qb : List{BehaviorWithId} .
  var M : Marking .
  var Q : Query .
  var Id : IdInstance .
  vars R1 R2 : Request .
  var RcvAns : Map{Request, Bool} .
  var Val : Bool .
 
  op  < nodeInventory: _, 
        instances: _, 
        connections: _,
        program: _,
        receivedAnswers: _, 
        outgoingAnswers: _, 
        outgoingRequests: _, 
        history: _,
        buffer: _ 
      > : 
      IdInstances Instances Connections Program 
      Map{Request, Bool} List{Request} List{Request} 
      Set{Request} List{Message} -> LocalConfiguration [ctor].

  --- some ops on configuration

  op isLocal(_,_) : Request IdInstances -> Bool .  
  --- determine whether a Request is related to a local treatement.
  eq isLocal([ dst: Id , query: Q ], Ids) = Id in Ids .

  op externFunCall(_,_) : Request Map{Request, Bool}  -> Bool .  
  --- get the evalaution of an external function related to a Request from Map{Request, Bool}
  eq externFunCall(R1, R1 |-> Val, RcvAns ) = Val .
  --- eq externFunCall(R1, RcvAns ) =  noValueYet [owise] .

  op localFunCall(_,_,_,_) : Request Instances Connections Program -> Bool .  
  ---- gives the evaluation of a local function related to Request according to certain parameters of a given configuration
  
  ---if the instance does not yet exist, then the active local function: isActiveLocal(Id1, Port) = false, otherwise it is the local active function within the instance. 
  eq localFunCall ([ dst: Id , query: isActive(Port) ],(Id |-> I1, Is), Cs, RP) =  isActiveLocal(I1, Port) .
  --- eq localFunCall ([ dst: Id , query: isActive(Port) ],Is,Cs,RP) =  noValueYet [owise] .

  ---if the instance does not yet exist, then the refusing local function: isRefusingLocal(Id1,Port) = true, otherwise it is the local refusing function within the instance.
  eq localFunCall ([ dst: Id , query: isRefusing(Port) ], (Id |-> I1, Is), Cs, RP) = isRefusingLocal(I1, Port) .
  ---eq localFunCall ([ dst: Id , query: isRefusing(Port) ], Ix, Lx, Px) =  noValueYet [owise] .
      
  eq localFunCall ([ dst: Id , query: isConnected(C) ], Is,Cs,RP) = C in Cs . 

  eq localFunCall ([ dst: Id , query: onDisconnect(C) ], Is,Cs,RP) = onDisconnectLocal(C,RP) . 
 
  eq localFunCall ([ dst: Id , query: isCompleted(IdBeh) ], (Id |-> { type: Ct,queueBehavior: Qb,marking: M },Is),Cs,RP) =  
  (not isBehaviorinList(IdBeh,Qb) ) and isCompletedLocal(Id,IdBeh,RP) .
  --- the evaluation of completed of an IdBeh depends on whether the push IdBeh exists in the program or in the behavior list.
  eq localFunCall ([ dst: Id , query: isCompleted(IdBeh) ], Is,Cs,RP) =  isCompletedLocal(Id,IdBeh,RP) [owise] . 
  --- special case the instance doesn't (the instance is not yet added or it is deleted)

  op funCall(_,_,_,_,_,_) :  Request IdInstances Map{Request, Bool} Instances Connections Program -> Bool . 
  --- gives the evaluation of a function related to Request according to the two cases, the function is local or external
  eq funCall(R1,Ids,RcvAns, Is,Cs,RP ) = if (isLocal(R1,Ids)) then localFunCall(R1,Is,Cs,RP) else externFunCall(R1,RcvAns) fi .

endfm