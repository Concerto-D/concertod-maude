
fmod CONCERTO-D-CONFIGURATION is 

  inc MESSAGE .
  inc INSTANCE .
  inc PROGRAM .
  inc CONNECTION .
  inc DECLARATION-SORTS . 

  pr SET{Question} . 
  pr MAP{Question, Bool} .
  pr LIST{Question} . 
  pr LIST{Message} .

  var Is : Instances .
  var I1 : Instance .
  var Ids : Ids .
  var IdBeh : Id .
  var Ct : ComponentType .
  var Port : Port .
  var Cs : Connections .
  var RP : Program .
  var C : Connection .
  var Qb : List{PushedBehavior} .
  var M : Marking .
  var Q : Query .
  var Id : Id .
  vars R1 R2 : Question .
  var eState : Map{Question, Bool} .
  var Val : Bool .
 
  op  < nodeInventory: _, 
        instances: _, 
        connections: _,
        program: _,
        externState: _,
        pendingQuestions: _, 
        outgoingQuestions: _, 
        history: _,
        incomingMsgs: _ 
      > : 
      Ids Instances Connections Program 
      Map{Question, Bool} List{Question} List{Question} 
      Set{Question} List{Message} -> LocalConfiguration 
      [ctor format(n++i s s d n++i s d ni s d ni s d ni s d ni s d ni s d ni s d ni s d s----i)].

  --- some ops on configuration

  op isLocal(_,_) : Question Ids -> Bool .  
  --- determine whether a Question is related to a local treatement.
  eq isLocal([ dst: Id , query: Q ], Ids) = Id in Ids .

  op externQuestion(_,_) : Question Map{Question, Bool}  -> Bool .  
  --- get the evalaution of an external function related to a Question from Map{Question, Bool}
  eq externQuestion(R1, R1 |-> Val, eState ) = Val .
  --- eq externFunCall(R1, eState ) =  noValueYet [owise] .

  op localQuestion(_,_,_,_) : Question Instances Connections Program -> Bool .  
  ---- gives the evaluation of a local function related to Question according to certain parameters of a given configuration
  
  ---if the instance does not yet exist, then the active local function: isActiveLocal(Id1, Port) = false, otherwise it is the local active function within the instance. 
  eq localQuestion ([ dst: Id , query: isActive(Port) ],(Id |-> I1, Is), Cs, RP) =  isActiveLocal(I1, Port) .
  --- eq localFunCall ([ dst: Id , query: isActive(Port) ],Is,Cs,RP) =  noValueYet [owise] .

  ---if the instance does not yet exist, then the refusing local function: isRefusingLocal(Id1,Port) = true, otherwise it is the local refusing function within the instance.
  eq localQuestion ([ dst: Id , query: isRefusing(Port) ], (Id |-> I1, Is), Cs, RP) = isRefusingLocal(I1, Port) .
  ---eq localFunCall ([ dst: Id , query: isRefusing(Port) ], Ix, Lx, Px) =  noValueYet [owise] .
      
  eq localQuestion ([ dst: Id , query: isConnected(C) ], Is,Cs,RP) = C in Cs . 

  eq localQuestion ([ dst: Id , query: onDisconnect(C) ], Is,Cs,RP) = onDisconnectLocal(C,RP) . 
 
  eq localQuestion ([ dst: Id , query: isCompleted(IdBeh) ], (Id |-> { type: Ct, queue: Qb, marking: M },Is),Cs,RP) =  
  (not isBehaviorinList(IdBeh,Qb) ) and isCompletedLocal(Id,IdBeh,RP) .
  --- the evaluation of completed of an IdBeh depends on whether the push IdBeh exists in the program or in the behavior list.
  eq localQuestion ([ dst: Id , query: isCompleted(IdBeh) ], Is,Cs,RP) =  isCompletedLocal(Id,IdBeh,RP) [owise] . 
  --- special case the instance doesn't (the instance is not yet added or it is deleted)

  op question(_,_,_,_,_,_) :  Question Ids Map{Question, Bool} Instances Connections Program -> Bool . 
  --- gives the evaluation of a function related to Question according to the two cases, the function is local or external
  eq question(R1,Ids,eState, Is,Cs,RP ) = if (isLocal(R1,Ids)) then localQuestion(R1,Is,Cs,RP) else externQuestion(R1,eState) fi .

endfm